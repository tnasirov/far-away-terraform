module "external_dns_irsa_role" {
  source                     = "terraform-aws-modules/iam/aws//modules/iam-role-for-service-accounts-eks"
  version                    = "5.10.0"
  create_role                = var.create_role_enabled
  role_name_prefix           = var.serviceaccount
  role_path                  = "/${var.cluster_name}/${var.namespace}/"
  attach_external_dns_policy = true

  oidc_providers = {
    ex = {
      provider_arn               = var.cluster_oidc_provider
      namespace_service_accounts = ["${var.namespace}:${var.serviceaccount}"]
    }
  }
  tags = merge(var.tags, {
    TerraformPath   = "far-away-terraform/aws/core-services/system-charts/external_dns"
  })
}

resource "helm_release" "external_dns" {
  name             = var.name
  chart            = "external-dns"
  repository       = "https://kubernetes-sigs.github.io/external-dns/"
  create_namespace = var.namespace == "kube-system" ? false : true
  version          = var.external_dns_chart_version
  namespace        = var.namespace
  values           = [local.base_values]

  dynamic "set" {
    for_each = var.extra_values
    content {
      name  = set.key
      value = set.value
    }
  }
}

